plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}
apply from: '../base.gradle'

android {
    defaultConfig {
        buildConfigField "String", "BASIC_AUTH_USER", "\"${getBasicAuthUser()}\""
        buildConfigField "String", "BASIC_AUTH_PASSWORD", "\"${getBasicAuthPassword()}\""
    }
    flavorDimensions 'environment', 'location'
    productFlavors {
        real {
            dimension 'environment'
        }
        mock {
            dimension 'environment'
        }
        albacete {
            dimension 'location'
        }
        valencia {
            dimension 'location'
        }
    }
    namespace 'com.vsa.filmoteca.network'
}

def getBasicAuthUser() {
    String user = System.getenv("BASIC_AUTH_USER")

    if (user == null) {
        println "BASIC AUTH USER NOT FOUND IN THE ENVIRONMENT"

        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        user = properties.getProperty("basicauth.user")

        if (user == null)
            println "BASIC AUTH USER NOT FOUND IN THE PROPERTIES FILE"
    }

    return user
}

def getBasicAuthPassword() {
    String password = System.getenv("BASIC_AUTH_PASSWORD")

    if (password == null) {
        println "BASIC AUTH PASSWORD NOT FOUND IN THE ENVIRONMENT"

        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        password = properties.getProperty("basicauth.password")

        if (password == null)
            println "BASIC AUTH PASSWORD NOT FOUND IN THE PROPERTIES FILE"
    }

    return password
}

dependencies {

    implementation project(path: ':core')

    // DEPENDENCY INJECTION
    implementation ProjectDependencies.hiltAndroid
    kapt ProjectDependencies.hiltCompiler

    // For instrumentation tests
    androidTestImplementation ProjectDependencies.hiltAndroidTesting
    kaptAndroidTest ProjectDependencies.hiltCompiler

    // For local unit tests
    testImplementation ProjectDependencies.hiltAndroidTesting
    kaptTest ProjectDependencies.hiltCompiler

    //COROUTINES
    implementation ProjectDependencies.coroutines

    implementation ProjectDependencies.okhttp
    implementation ProjectDependencies.okhttpLoggingInterceptor
    api ProjectDependencies.retrofit
    implementation ProjectDependencies.retrofitConverterScalars
}